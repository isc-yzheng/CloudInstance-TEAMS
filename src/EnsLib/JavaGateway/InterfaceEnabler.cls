/* Copyright (c) 2020 by InterSystems Corporation.
   Cambridge, Massachusetts, U.S.A.  All rights reserved.
   Confidential property of InterSystems Corporation. */

Class EnsLib.JavaGateway.InterfaceEnabler Extends %Net.Remote.ImportHelper [ System = 3 ]
{

/// Imports sample Java classes from //dev/java/samples
ClassMethod ImportJGSamples(port As %Integer, host As %String = "127.0.0.1") [ Final ]
{
	Set classPath=##class(%ListOfDataTypes).%New()
	Do classPath.Insert($SYSTEM.Util.InstallDirectory()_"dev/java/samples/")
	Do ..Import("remote.test.Student",classPath,port,host)
}

/// Imports the JDBC interface
ClassMethod ImportJDBC(port As %Integer, host As %String = "127.0.0.1") [ Final ]
{
	Do ..Import("java.sql.DriverManager","",port,host)
}

/// Imports all J2EE interfaces (EJB, JCA, JTA, Java XML, JTA,...)
/// J2EEJarFile is the location/name of your J2EE jar file, say "c:/myj2ee/j2ee.jar"
ClassMethod ImportJ2EE(J2EEJarFile As %String, port As %Integer, host As %String = "127.0.0.1") [ Final ]
{
	Set classPath=##class(%ListOfDataTypes).%New()
	Do classPath.Insert(J2EEJarFile)	
	Do ..Import(J2EEJarFile,classPath,port,host)
}

/// Imports SonicMQ QueueConnectionFactory, allowing connectivity to SonicMQ JMS Server
/// sonicBrokerJar should point to your Sonic broker.jar, say "C:/Program Files/SonicSoftware/SonicMQ/lib/broker.jar"
/// sonicContextJar should point to your Sonic mfcontext.jar, say "C:/Program Files/SonicSoftware/SonicMQ/lib/mfcontext.jar"
ClassMethod ImportSonicJMS(sonicBrokerJar As %String, sonicContextJar As %String, port As %Integer, host As %String = "127.0.0.1") [ Final ]
{
	Set classPath=##class(%ListOfDataTypes).%New()
	Do classPath.Insert(sonicBrokerJar)
	Do classPath.Insert(sonicContextJar)
	Do ..Import("progress.message.jclient.TopicConnectionFactory",classPath,port,host)	
	Do ..Import("progress.message.jclient.QueueConnectionFactory",classPath,port,host)
	Do ..Import("javax.jms.DeliveryMode",classPath,port,host)
}

/// Imports InterSystems JBoss Person EJBs
/// J2EEJarFile should point to the J2EE jar file, for example "c:/myj2ee/j2ee.jar"
/// PersonEJBJar should point to your PersonEJB.jar (as generated by the EJB Boss projection)
/// for example: "c:/jboss-4.0.1sp1/server/default/deploy/PersonEJB.jar"
ClassMethod ImportPersonJBoss(PersonEJBJar As %String, J2EEJarFile As %String, port As %Integer, host As %String = "127.0.0.1") [ Final ]
{
	Set classPath=##class(%ListOfDataTypes).%New()
	Do classPath.Insert(J2EEJarFile)
	Do classPath.Insert(PersonEJBJar)
	Do ..Import("Sample.EJBPerson_Home",classPath,port,host)	
	Do ..Import("javax.naming.InitialContext",classPath,port,host)
	Do ..Import("javax.rmi.PortableRemoteObject",classPath,port,host)
}

/// Imports the SAP JCo interface
/// SAPJCoJar should point to your SAP JCo jar file, say C:/SAP/sapjco.jar
ClassMethod ImportSAPJCo(SAPJCoJar As %String, port As %Integer, host As %String = "127.0.0.1") [ Final ]
{
	Set classPath=##class(%ListOfDataTypes).%New()	
	Do classPath.Insert(SAPJCoJar)

	// Exclude all SAP utility classes
	Set exclusions=##class(%ListOfDataTypes).%New()
	Do exclusions.Insert("com.sap.mw.jco.util")
	Do exclusions.Insert("com.sap.mw.jco.support")
	Do exclusions.Insert("com.sap.mw.jco.rfc")
	Do ..Import(SAPJCoJar,classPath,port,host,exclusions)
}

}
